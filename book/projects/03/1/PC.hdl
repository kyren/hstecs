// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/1/PC.hdl

/**
 * 16-bit counter with load and reset controls.
 *
 * If reset(t-1) then out(t) = 0
 *    else if load(t-1) then out(t) = in(t-1)
 *         else if inc(t-1) then out(t) = out(t-1) + 1 (integer addition)
 *              else out(t) = out(t-1)
 */

CHIP PC {

    IN  in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Register(in=rin, load=true, out=rout, out=out);

    Add16(a=rout, b[0]=inc, b[1..15]=false, out=rinc);
    Mux16(a=rinc, b=in, sel=load, out=lval);
    Mux16(a=lval, b=false, sel=reset, out=rin);
}
