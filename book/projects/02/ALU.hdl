// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU.  Computes a pre-defined set of functions out = f(x,y)
 * where x and y are two 16-bit inputs. The function f is selected 
 * by a set of 6 control bits denoted zx, nx, zy, ny, f, no.
 * The ALU operation can be described using the following pseudocode:
 *     if zx=1 set x = 0       // 16-bit zero constant
 *     if nx=1 set x = !x      // Bit-wise negation
 *     if zy=1 set y = 0       // 16-bit zero constant
 *     if ny=1 set y = !y      // Bit-wise negation
 *     if f=1  set out = x + y // Integer 2's complement addition
 *     else    set out = x & y // Bit-wise And
 *     if no=1 set out = !out  // Bit-wise negation
 *
 * In addition to computing out, the ALU computes two 1-bit outputs:
 *     if out=0 set zr = 1 else zr = 0 // 16-bit equality comparison
 *     if out<0 set ng = 1 else ng = 0 // 2's complement comparison
 */

CHIP ALU {

    IN  // 16-bit inputs:
        x[16], y[16],
        // Control bits:
        zx, // Zero the x input
        nx, // Negate the x input
        zy, // Zero the y input
        ny, // Negate the y input
        f,  // Function code: 1 for add, 0 for and
        no; // Negate the out output

    OUT // 16-bit output
        out[16],

        // ALU output flags
        zr, // 1 if out=0, 0 otherwise
        ng; // 1 if out<0, 0 otherwise

    PARTS:

    Mux16(a=x, b=false, sel=zx, out=xin);
    Not16(in=xin, out=xnot);
    Mux16(a=xin, b=xnot, sel=nx, out=xout);

    Mux16(a=y, b=false, sel=zy, out=yin);
    Not16(in=yin, out=ynot);
    Mux16(a=yin, b=ynot, sel=ny, out=yout);

    Add16(a=xout, b=yout, out=addres);
    And16(a=xout, b=yout, out=andres);

    Mux16(a=andres, b=addres, sel=f, out=res);
    Not16(in=res, out=resnot);

    Mux16(a=res, b=resnot, sel=no, out=out, out[15]=ng, out[0..7]=out0, out[8..15]=out1);

    Or8Way(in=out0, out=nz0);
    Or8Way(in=out1, out=nz1);
    Or(a=nz0, b=nz1, out=nz);
    Not(in=nz, out=zr);
}
